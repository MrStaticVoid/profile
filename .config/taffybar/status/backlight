#!/usr/bin/env zsh

SELF="$0"
source "$(dirname "$(readlink -f "$SELF")")/lib.zsh" "$SELF"

wait_for_events() {
    while event="$(get_event)"; do
        if [[ $event == *'property Backlight'*'state NewValue' ]]; then
            break
        fi
    done
}

refresh() {
    timeout 2 "$SELF" wait
    if (( ? != 124 )); then
        local brightness="$(qdbus org.kde.Solid.PowerManagement /org/kde/Solid/PowerManagement/Actions/BrightnessControl brightness)"
        local max_brightness="$(qdbus org.kde.Solid.PowerManagement /org/kde/Solid/PowerManagement/Actions/BrightnessControl brightnessMax)"
        local percentage="$(printf "%.0f" $((brightness * 100.0 / max_brightness)))"
        [[ $percentage ]] && segment "$(fontawesome "\uf0eb") ${percentage}%"
    fi
}

change_brightness() {
    local current="$(qdbus org.kde.Solid.PowerManagement /org/kde/Solid/PowerManagement/Actions/BrightnessControl brightness)"
    local max="$(qdbus org.kde.Solid.PowerManagement /org/kde/Solid/PowerManagement/Actions/BrightnessControl brightnessMax)"
    local steps="$(qdbus org.kde.Solid.PowerManagement /org/kde/Solid/PowerManagement/Actions/BrightnessControl brightnessSteps)"
    local increment=$((max * 1.0 / steps))

    # Snap the current value to the nearest increment
    current=$((current / increment))
    current="$(printf '%.0f' "$current")"
    current=$((current * increment))

    [[ $1 == 'decrease' ]] && increment=$((-increment))

    local new=$((current + increment))

    if (( new > max )); then
        new=$max
    elif (( new < 0 )); then
        new=0
    fi

    qdbus org.kde.Solid.PowerManagement /org/kde/Solid/PowerManagement/Actions/BrightnessControl setBrightness "$(printf '%.0f' "$new")"
}

case "$1" in
    'monitor')
        start_monitor_loop xev -root -event randr
        ;;

    'wait')
        wait_for_events
        ;;

    'increase')
        change_brightness increase
        ;;

    'decrease')
        change_brightness decrease
        ;;

    *)
        refresh
        ;;
esac
