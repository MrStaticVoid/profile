#!/usr/bin/env zsh

SELF="$0"
source "$(dirname "$(readlink -f "$SELF")")/lib.zsh" "$SELF"

wait_for_events() {
    while event="$(get_event)"; do
        if [[ $event != Monitoring* ]]; then
            return
        fi
    done
}

battery_icon() {
    local percentage="$1" icon

    if (( percentage > 80 )); then
        icon="\uf240"
    elif (( percentage > 60 )); then
        icon="\uf241"
    elif (( percentage > 40 )); then
        icon="\uf242"
    elif (( percentage > 20 )); then
        icon="\uf243"
    else
        icon="\uf244"
    fi

    fontawesome "$icon"
}

main() {
    if get_state > /dev/null; then
        timeout 5 "$SELF" wait
    fi

    upower --enumerate | grep '/battery_' | while read battery_object; do
        info="$(upower --show-info "$battery_object")"
        state="$(awk '/state:/ { print $2 }' <<< "$info")"
        percentage="${$(awk '/percentage:/ { print $2 }' <<< "$info")%\%}"

        case "$state" in
            'discharging')
                tte="${$(awk '/time to empty:/ { print $4 substr($5, 0, 1) }' <<< "$info"):-...}"
                segment "$(battery_icon "$percentage") ${tte}"
                ;;

            'charging')
                segment "$(fontawesome "\uf0e7") ${percentage}%"
                ;;

            *)
                ;;
        esac
    done

    set_state 1
}

case "$1" in
    'monitor')
        start_monitor_loop upower --monitor
        ;;

    'wait')
        wait_for_events
        ;;

    *)
        main
        ;;
esac
