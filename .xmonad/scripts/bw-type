#!/usr/bin/env zsh
#
# Bitwarden CLI helper for X shortcuts
#
# Pass the name and ID of a password stored in Bitwarden and the script will
# log in if necessary, then type the password, and provide notifications along
# the way with libnotify.
#

if [[ $# != 2 ]]; then
    echo "Usage: ${0} NAME ID" >&2
    exit 1
fi

NAME="$1"
ID="$2"

notify() {
    notify-send --icon=password-copy "$NAME" "$@"
}

unlock_vault() {
    if password="$(ksshaskpass -- Bitwarden)"; then
        if bw_unlock_output="${$(bw unlock --raw <<< "$password" 2>&1)##*$'\n'}"; then
            systemctl --user set-environment BW_SESSION="$bw_unlock_output"
            bw --nointeraction sync
        else
            notify "$bw_unlock_output"
            [[ $bw_unlock_output == 'You are not logged in.' ]] && exit 1
        fi
    else
        # Clicked the dialog cancel button
        exit 0
    fi
}

notify 'Typing...'

while true; do
    if bw_password_output="${$(bw --nointeraction get password "$ID" 2>&1)##*$'\n'}"; then
        type-line <<< "$bw_password_output"
        exit 0
    elif [[ $bw_password_output == 'Vault is locked.' ]]; then
        unlock_vault
    else
        notify "$bw_password_output"
        exit 1
    fi
done
